//file:noinspection GroovyAssignabilityCheck
//file:noinspection GrDeprecatedAPIUsage
plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.modrinth.minotaur" version "2.+"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation libs.fabric.loader

    modApi libs.architectury.fabric
    modApi libs.mod.menu
    modApi(libs.pehkui) {
        exclude group: "net.fabricmc.fabric-api"
    }
    modApi(libs.cloth.config.fabric) {
        exclude group: "net.fabricmc.fabric-api"
    }

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
    common(project(path: ":fabric-like", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":fabric-like", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

jar {
    archivesBaseName = project.archivesBaseName + "-fabric"
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

curseforge {
    if (System.getenv("CURSEFORGE_API_KEY") != null) {
        println("Enabling Curseforge publishing for Fabric")
        apiKey = System.getenv("CURSEFORGE_API_KEY")

        project {
            id = "485532"
            changelogType = "markdown"
            releaseType = "release"
            changelog = rootProject.file("CHANGELOG.md")

            mainArtifact(remapJar.archiveFile) {
                displayName = rootProject.property("release_name").replace("\$LOADER", "Fabric")
            }

            afterEvaluate {
                uploadTask.dependsOn("build")
            }

            addGameVersion "Fabric"
            addGameVersion libs.versions.minecraft.get()

            relations {
                requiredDependency "fabric-api"
                requiredDependency "architectury-api"
                requiredDependency "cloth-config"
                requiredDependency "pehkui"
            }
        }

        options {
            forgeGradleIntegration = false
        }
    }
}

modrinth {
    syncBodyFrom = rootProject.file("README.md").text

    if (System.getenv("MODRINTH_API_KEY") != null) {
        println("Enabling Modrinth publishing for Fabric")

        token = System.getenv("MODRINTH_API_KEY")
        projectId = "reaping"
        versionName = rootProject.property("release_name").replace("\$LOADER", "Fabric")
        changelog = rootProject.file("CHANGELOG.md").text
        uploadFile = remapJar
        loaders = ["fabric"]
        gameVersions = [libs.versions.minecraft.get()]

        dependencies {
            required.project "fabric-api"
            required.project "architectury-api"
            required.project "cloth-config"
            required.project "pehkui"
        }
    }
}
