//file:noinspection GroovyAssignabilityCheck
plugins {
    id "org.cadixdev.licenser" version "0.6.1"
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.0-SNAPSHOT" apply false
    id "com.github.breadmoirai.github-release" version "2.2.12"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft libs.minecraft
        mappings variantOf(libs.quilt.mappings) { classifier("intermediary-v2") }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        maven { url "https://maven.shedaniel.me/" }
        maven { url "https://api.modrinth.com/maven" }
        maven { url "https://maven.terraformersmc.com/releases" }
        maven { url "https://jitpack.io/" }
        maven { url "https://maven.quiltmc.org/repository/release" }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        //noinspection GroovyAccessibility
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}

githubRelease {
    if (System.getenv("GITHUB_TOKEN") != null) {
        println("Enabled GitHub release publishing")

        token = System.getenv("GITHUB_TOKEN")
        owner = "JamCoreModding"
        repo = "Reaping"
        targetCommitish = "1.19"
        tagName = rootProject.property("mod_version")
        releaseName = rootProject.property("release_name").replace("\$LOADER", "Fabric, Quilt, & Forge")
        body = rootProject.file("CHANGELOG.md").getText() +
                "\n Release files can be found on [Curseforge](https://www.curseforge.com/minecraft/mc-mods/reaping/files) or [Modrinth](https://modrinth.com/mod/reaping)"
        prerelease = false
        draft = true
    }
}

tasks.named("publish") {
    dependsOn "fabric:curseforge"
    dependsOn "quilt:curseforge"
    dependsOn "forge:curseforge"
    dependsOn "fabric:modrinth"
    dependsOn "quilt:modrinth"
    dependsOn "forge:modrinth"
    dependsOn "githubRelease"
}
