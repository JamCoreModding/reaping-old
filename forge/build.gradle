plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"

    modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"
    modApi "me.shedaniel.cloth:cloth-config-forge:${rootProject.cloth_config_version}"
    modApi "com.github.Virtuoel:Pehkui:${rootProject.pehkui_forge}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

processResources {
    inputs.property "github_url", rootProject.property("github_url")
    inputs.property "license", rootProject.property("license")
    inputs.property "id", rootProject.property("id")
    inputs.property "version", project.version
    inputs.property "name", rootProject.property("name")
    inputs.property "author", rootProject.property("author")
    inputs.property "description", rootProject.property("description")

    filesMatching("META-INF/mods.toml") {
        expand "github_url": rootProject.property("github_url"),
                "license": rootProject.property("license"),
                "id": rootProject.property("id"),
                "version": project.version,
                "name": rootProject.property("name"),
                "author": rootProject.property("author"),
                "description": rootProject.property("description")
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

curseforge {
    if (System.getenv("CURSEFORGE_API_KEY") != null) {
        println("Enabling Curseforge publishing for Forge")
        apiKey = System.getenv("CURSEFORGE_API_KEY")

        project {
            id = "485532"
            changelogType = "markdown"
            releaseType = "release"
            changelog = rootProject.file("CHANGELOG.md")

            mainArtifact(remapJar.archiveFile) {
                displayName = rootProject.property("release_name").replace("\$LOADER", "Forge")
            }

            afterEvaluate {
                uploadTask.dependsOn("build")
            }

            addGameVersion "Forge"
            addGameVersion "1.18.2"

            relations {
                requiredDependency "architectury-api"
                requiredDependency "cloth-config"
            }
        }

        options {
            forgeGradleIntegration = false
        }
    }
}
